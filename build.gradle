plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'com.github.spotbugs' version '6.0.4'
    id 'net.ltgt.errorprone' version '3.1.0'
}

group 'com.github.akunzai'
version '3.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.sendgrid:sendgrid-java:4.10.1'
    implementation 'jakarta.mail:jakarta.mail-api:2.1.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.0'
    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.22.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'com.lmax:disruptor:4.0.0' // required for log4j2 AsyncLogger
    testImplementation 'com.sun.mail:javax.mail:1.6.2' // required for log4j2 SMTPAppender
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    spotbugs 'com.github.spotbugs:spotbugs:4.8.3'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    errorprone 'com.google.errorprone:error_prone_core:2.23.0'

    constraints {
        implementation('com.fasterxml.jackson.core:jackson-databind:2.16.0') {
            // Use the latest version whenever possible
            because 'previous versions have known security vulnerabilities'
        }
        implementation('commons-codec:commons-codec:1.16.0') {
            // Use the latest version whenever possible
            because 'previous versions have known security vulnerabilities'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    // Error Prone require JDK 11 since version 2.11
    // https://github.com/google/error-prone/releases/tag/v2.11.0
    options.errorprone.enabled = JavaVersion.current().isJava11Compatible()
    // Workaround to compiling with JDK 11 by disable the HidingField check
    // https://github.com/google/error-prone/issues/2092
    options.errorprone.errorproneArgs.addAll("-Xep:HidingField:OFF")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

spotbugs {
    excludeFilter = file("$projectDir/spotbugs-exclude.xml")
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'log4j2-sendgrid-appender'
                description = 'Send log4j2 errors via SendGrid service'
                url = 'https://github.com/akunzai/log4j2-sendgrid-appender'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'akunzai'
                        name = 'Charley Wu'
                        email = 'akunzai@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/akunzai/log4j2-sendgrid-appender.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty('sonatype.user') ?: System.getenv('SONATYPE_USER')
                password = project.findProperty('sonatype.token') ?: System.getenv('SONATYPE_TOKEN')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task runTestRunner(type: JavaExec) {
    group = 'Verification'
    description 'Run the test runner'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.github.akunzai.log4j.SendGridTestRunner'
    // https://logging.apache.org/log4j/2.x/manual/async.html#AllAsync
    systemProperties = ['log4j2.contextSelector': 'org.apache.logging.log4j.core.async.AsyncLoggerContextSelector']
}