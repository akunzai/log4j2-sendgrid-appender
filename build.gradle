plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.github.spotbugs' version '4.6.0'
}

group 'com.github.akunzai'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation "com.sendgrid:sendgrid-java:4.0.0"
    implementation "com.sun.mail:javax.mail:1.5.0"
    implementation "org.apache.logging.log4j:log4j-core:2.9.0"
    annotationProcessor "org.apache.logging.log4j:log4j-core:2.9.0"
    testImplementation "com.sendgrid:sendgrid-java:4.3.0"
    testImplementation "junit:junit:4.12"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                description = 'Send log4j2 errors via SendGrid service'
                url = 'https://github.com/akunzai/log4j2-sendgrid-appender'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'akunzai'
                        name = 'Charley Wu'
                        email = 'akunzai@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/akunzai/log4j2-sendgrid-appender'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Send log4j2 errors via SendGrid service'
        websiteUrl = 'https://github.com/akunzai/log4j2-sendgrid-appender'
        issueTrackerUrl = 'https://github.com/akunzai/log4j2-sendgrid-appender/issues'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/akunzai/log4j2-sendgrid-appender.git'
        githubRepo = 'akunzai/log4j2-sendgrid-appender'
        labels = ['log4j','sendgrid']
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}